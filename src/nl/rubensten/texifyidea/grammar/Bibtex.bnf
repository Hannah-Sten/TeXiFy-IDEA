{
    parserClass="nl.rubensten.texifyidea.parser.BibtexParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Bibtex"
    psiImplClassSuffix="Impl"
    psiPackage="nl.rubensten.texifyidea.psi"
    psiImplPackage="nl.rubensten.texifyidea.psi.impl"

    elementTypeHolderClass="nl.rubensten.texifyidea.psi.BibtexTypes"
    elementTypeClass="nl.rubensten.texifyidea.psi.BibtexElementType"
    tokenTypeClass="nl.rubensten.texifyidea.psi.BibtexTokenType"
}

bibtexFile ::= (entry | comment)* SEPARATOR?

entry ::= SEPARATOR? type (OPEN_BRACE | OPEN_PARENTHESIS) ((id? (tag SEPARATOR)* tag ENDTRY) | preamble CLOSE_BRACE)

type ::= TYPE_TOKEN

preamble ::= (quoted_string (CONCATENATE quoted_string)+) | quoted_string | NUMBER | IDENTIFIER

id ::= comment* IDENTIFIER comment* SEPARATOR

tag ::= comment* key comment* ASSIGNMENT comment* content comment*

key ::= IDENTIFIER

content ::= (string (CONCATENATE string)+) | string | NUMBER | IDENTIFIER

string ::= defined_string | quoted_string | braced_string

defined_string ::= IDENTIFIER

quoted_string ::= QUOTES normal_text END_QUOTES

braced_string ::= OPEN_BRACE normal_text+ CLOSE_BRACE

comment ::= COMMENT_TOKEN

normal_text ::= NORMAL_TEXT_WORD+