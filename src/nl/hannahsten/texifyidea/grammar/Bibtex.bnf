{
    parserClass="nl.hannahsten.texifyidea.parser.BibtexParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Bibtex"
    psiImplClassSuffix="Impl"
    psiPackage="nl.hannahsten.texifyidea.psi"
    psiImplPackage="nl.hannahsten.texifyidea.psi.impl"

    elementTypeHolderClass="nl.hannahsten.texifyidea.psi.BibtexTypes"
    elementTypeClass="nl.hannahsten.texifyidea.psi.BibtexElementType"
    tokenTypeClass="nl.hannahsten.texifyidea.psi.BibtexTokenType"
}

bibtexFile ::= (entry | comment)*

entry ::= type (OPEN_BRACE | OPEN_PARENTHESIS) (id? entry_content | preamble) comment* endtry comment* SEPARATOR?  { pin=1 }

entry_content ::= tag (SEPARATOR tag)* SEPARATOR? { pin=1 }

endtry ::= SEPARATOR? comment* (CLOSE_BRACE | CLOSE_PARENTHESIS)

type ::= TYPE_TOKEN

preamble ::= (quoted_string (CONCATENATE quoted_string)*) | NUMBER | IDENTIFIER

id ::= comment* IDENTIFIER comment* SEPARATOR { pin=2 }

tag ::= comment* key comment* ASSIGNMENT comment* content comment* { pin=2 }

key ::= IDENTIFIER

content ::= (string (CONCATENATE string)+) | string | NUMBER | IDENTIFIER

string ::= defined_string | quoted_string | braced_string

defined_string ::= IDENTIFIER

quoted_string ::= QUOTES normal_text? END_QUOTES { pin=1 }

braced_string ::= OPEN_BRACE normal_text+ CLOSE_BRACE { pin=1 }

comment ::= COMMENT_TOKEN

normal_text ::= NORMAL_TEXT_WORD+